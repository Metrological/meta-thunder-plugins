# Provides the WPE Compositor plugin & client settings

# Additional flags
WPE_COMPOSITOR_AUTOSTART ??= "true"
WPE_COMPOSITOR_OUTOFPROCESS ??= "true"
WPE_COMPOSITOR_RESOLUTION ??= "720p"

# Additional flags for Wayland implementations
WPE_COMPOSITOR_VIRTUALINPUT ??= "ON"
WPE_COMPOSITOR_CURSOR_FILE ??= ""

PACKAGECONFIG:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'wayland', 'compositor_wayland', '', d)}"
PACKAGECONFIG:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'wayland', '', bb.utils.contains('PREFERRED_PROVIDER_virtual/mesa', 'mesa', 'compositor_mesa', '', d), d)}"

# Choose westeros or weston dependency
WPE_COMPOSITOR_WAYLAND_SUB_IMPL ??= "${@bb.utils.contains('DISTRO_FEATURES', 'weston', 'Weston', 'Westeros', d)}"
WPE_COMPOSITOR_WAYLAND_DEP ??= "${@bb.utils.contains('DISTRO_FEATURES', 'weston', 'weston wayland', 'westeros', d)}"

# Repo details
WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_PATH ??= ""
WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_REPOSITORY ??= "https://code.rdkcentral.com/r/soc/broadcom/components/rdkcentral/thundernanoservices/Compositor"
WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_VERSION ??= "master"

PACKAGECONFIG[compositor] = "\
    -DPLUGIN_COMPOSITOR=ON \
    -DPLUGIN_COMPOSITOR_AUTOSTART=${WPE_COMPOSITOR_AUTOSTART} \
    -DPLUGIN_COMPOSITOR_OUTOFPROCESS=${WPE_COMPOSITOR_OUTOFPROCESS} \
    -DPLUGIN_COMPOSITOR_RESOLUTION=${WPE_COMPOSITOR_RESOLUTION} \
    ,-DPLUGIN_COMPOSITOR=OFF,wpeframework-clientlibraries \
"

PACKAGECONFIG[compositor_wayland] = "\
    -DPLUGIN_COMPOSITOR_IMPLEMENTATION='Wayland' \
    -DCOMPOSITOR_WAYLAND_IMPLEMENTATION_PATH=${WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_PATH} \
    -DCOMPOSITOR_WAYLAND_IMPLEMENTATION_REPOSITORY=${WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_REPOSITORY} \
    -DCOMPOSITOR_WAYLAND_IMPLEMENTATION_VERSION=${WPE_COMPOSITOR_WAYLAND_IMPLEMENTATION_VERSION} \
    -DPLUGIN_COMPOSITOR_SUB_IMPLEMENTATION=${WPE_COMPOSITOR_WAYLAND_SUB_IMPL} \
    -DPLUGIN_COMPOSITOR_CURSOR_FILE=${WPE_COMPOSITOR_CURSOR_FILE} \
    -DPLUGIN_COMPOSITOR_VIRTUALINPUT=${WPE_COMPOSITOR_VIRTUALINPUT} \
    ,,${WPE_COMPOSITOR_WAYLAND_DEP} \
"
# To select the Mesa compositor:
# Remove Wayland from DISTRO_FEATURES in local.conf:
# DISTRO_FEATURES:remove = "wayland"
PACKAGECONFIG[compositor_mesa] = "\
    -DPLUGIN_COMPOSITOR_IMPLEMENTATION='Mesa' \
    -DCOMPOSITOR_BACKEND_IMPLEMENTATION='DRM' \
    -DCOMPOSITOR_BUFFER_IMPLEMENTATION='GBM' \
    -DPLUGIN_COMPOSITOR_RENDER_NODE='/dev/dri/renderD128' \
    -DPLUGIN_COMPOSITOR_THREAD_COUNT='2' \
    -DENABLE_ATOMIC='ON' \
    ,,libdrm mesa libpng \
"

PACKAGECONFIG[vc4graphics] = "-DVC6=ON,,libdrm mesa"

